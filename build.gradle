

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.rodm:gradle-teamcity-plugin:1.0"
    }
}


ext.teamcityVersion = hasProperty('teamcity.version') ? property('teamcity.version') : '2021.1-SNAPSHOT'

ext.teamcityDir = hasProperty('teamcity.dir') ? property('teamcity.dir') : "$rootDir/teamcity/servers/TeamCity-${teamcityVersion}"
ext.teamcityDataDir = "$rootDir/teamcity/data/" + teamcityVersion
ext.teamcityJavaHome = System.properties['java.home']

ext.amazonUtilVersion = hasProperty('amazon.util.version') ? property('amazon.util.version') : teamcityVersion

apply plugin: 'idea'
idea {
    project {
        vcs = 'Git'
    }
}

subprojects {
    group = 'jetbrains.buildServer.codedeploy'
}

configure(subprojects) {
    apply plugin: 'java'

    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    test {
        useTestNG()
        testLogging.showStandardStreams = true
    }

    repositories {
        mavenCentral()
        maven {
            url = 'https://repository.jetbrains.com/all'
        }
    }
}
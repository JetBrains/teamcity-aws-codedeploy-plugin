/*
 * Copyright 2000-2016 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.github.rodm.teamcity-agent'
apply plugin: 'com.github.rodm.teamcity-server'

dependencies {
    agent project(':amazon-util')
    agent project(':aws-codedeploy-common')
    agent project(':aws-codedeploy-agent')

    server project(':amazon-util')
    server project(':aws-codedeploy-common')
    server project(':aws-codedeploy-server')
}

def awsSDKFiles = {project(':amazon-util').configurations.compile.files({it.group == 'com.amazonaws' && it.name == 'aws-java-sdk'})}

teamcity {
    agent {
        descriptor = project(':aws-codedeploy-agent').file('teamcity-plugin.xml')
        files {
            into('lib') {
                from awsSDKFiles()
            }
        }
    }

    server {
        descriptor = file("$rootDir/teamcity-plugin.xml")
        tokens = [Plugin_Version: 'SNAPSHOT-' + new Date().format('yyyyMMddHHmm')]
        files {
            into('server') {
                from awsSDKFiles()
            }
        }

        environments {
            teamcity {
                homeDir = file(teamcityDir)
                dataDir = file(teamcityDataDir)
                javaHome = file(teamcityJavaHome)
            }
        }
    }

    version = teamcityVersion
}
